grammar org.vanderveen.ev3rt.Dsl with org.eclipse.xtext.common.Terminals

generate behaviourDSL "erinvanderveen.nl/behaviourDSL"

Mission:
	'Mission' name = ID
	'Behaviors:' behaviours += Behaviour+
;

Behaviour:
	name = ID
	('Conditions:' conditions += Condition*)?
	'Actions:' action += Action+
;

// Conditions
Condition:
	sensor = Sensor
	operator = Operator
	value = Value
;

enum Sensor:
	Touch = 'touchSensor' |
	Color = 'colorSensor' |
	Sonic = 'ultrasonicSensor'
;

enum Operator:
	is = 'equals' |
	isnot = 'not equals' |
	greater = 'is greater than' |
	lower = 'is lower than'
;

Value:
	(pressed = PressedState | color = Color | distance = Distance)
;

enum PressedState:
	Unpressed | Pressed
;

enum Color:
	White | Black | Blue | Red | Yellow
;

Distance:
	cm = INT 'cm'
;

// Actions
Action:
	action = (Move | Turn | Sound)// | Light)
;

Move:
	'Move' direction = Direction duration = Duration
;

Turn:
	'Turn' direction = Direction
;

Sound:
	'Play' 'sound' tone = Tone duration = Duration
;

Tone:
	frequency = INT 'Hz'
;

enum Direction:
	Forward = 'forward' |
	Backward = 'backward' |
	Left = 'left' |
	Right = 'right'
;

Duration:
	time = INT 'milliseconds'
;
